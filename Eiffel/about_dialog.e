note
	description: "About dialog box"
	author: "Generated by the New Vision2 Application Wizard."
	date: "$Date: 2018/5/16 14:13:59 $"
	revision: "1.0.0"

class
	SELECT_NAME_DIALOG

inherit
	EV_DIALOG
		redefine
			create_interface_objects,
			initialize
		end

	INTERFACE_NAMES
		export
			{NONE} all
		undefine
			default_create, copy
		end

create
	default_create

feature {ANY}
	ok_actions: ACTION_SEQUENCE[STRING]

feature {NONE} -- Implementation

	text_field: EV_TEXT_FIELD
			-- "OK" button.

feature {NONE} -- Initialization

	create_interface_objects
			-- <Precursor>
		do
			Precursor

			create text_field
			create ok_actions
		end

	initialize
			-- Populate the dialog box.
		local
			main_vertical_box: EV_VERTICAL_BOX
			information_label: EV_LABEL
			ok_button: EV_BUTTON
			cancel_button: EV_BUTTON
			buttons_box: EV_HORIZONTAL_BOX
		do
			Precursor
			close_request_actions.extend(agent on_cancel)

			create information_label
			information_label.set_text (Label_select_player_name)
			information_label.align_text_left

			create ok_button.make_with_text (Button_ok_item)
			ok_button.set_minimum_size (75, 24)
			ok_button.select_actions.extend (agent on_ok)

			create cancel_button.make_with_text (Button_cancel_item)
			cancel_button.set_minimum_size (75, 24)
			cancel_button.select_actions.extend (agent on_cancel)

			create buttons_box
			buttons_box.extend (create {EV_CELL}) -- Fill in empty space on left
			buttons_box.extend (cancel_button)
			buttons_box.extend (ok_button)
			buttons_box.disable_item_expand (ok_button)

			create main_vertical_box
			main_vertical_box.set_border_width (7)
			main_vertical_box.extend (information_label)
			main_vertical_box.extend (text_field)
			main_vertical_box.extend (buttons_box)
			extend (main_vertical_box)

			set_default_push_button (ok_button)
			set_default_cancel_button (cancel_button)

			set_title (Title_select_player_name)
			set_width(300)
			set_position(0, 200)
		end

		on_ok
			local
				warning: EV_WARNING_DIALOG
			do
				if text_field.text_length > 0 then
					ok_actions.call(text_field.text.as_string_8)
					destroy
				else
					create warning.make_with_text (Message_no_name_set)
					warning.show_modal_to_window (current)
				end
			end

		on_cancel
			do
				destroy
			end
end
